title: PostOfficeIntro
---
///what package Catto is holding
<<declare $holding = "nothing">>
<<declare $packagesDeliveredCount = 0>>
<<declare $playedPOIntro = false>>
<<declare $packageAssigned = false>>
<<set $holding = "nothing">>
<<set $hasNotMetAssistant = false>>
Assistant Cat: Good Meowning Catto!
Assistant Cat: I hope you slept well
Assistant Cat: Here’s some news for today
Assistant Cat: The sun was so big today
Assistant Cat: It looked like the egg I had for breakfast!
Assistant Cat: Have I told you about how nutritious eggs are by the way?
Assistant Cat: The protein to calorie ratio is just off the charts!
Assistant Cat: Speaking of which, your hat kind of looks like…
Assistant Cat: Anyway!
Assistant Cat: I think that’s all for news
Assistant Cat: Are you ready for your first delivery?
-> Catto:  Is there aaaaaaaaanything else you forgot to mention?
	Assistant Cat: Nope, I think that’s all!
	-> Catto: I’m ready to start the day!
	-> Catto: Nothing else? Not even any birthdays?????
		Assistant Cat: I never forget a birthday
		Assistant Cat: Are you ready to start your day?
		-> Catto: I’m ready to start the day!
-> Catto: Thanks, I’m ready to start the day!
Assistant Cat: I have a few packages I still need to process
Assistant Cat: I have this one ready to be delivered
<<Trigger PackageBakery>>
<<declare $bakeryPackageDelivered = false>>
<<set $bakeryPackageDelivered = false>>
Assistant Cat: I hope you’ve stretched
Assistant Cat: You’re going to need to take this one up the hill to the bakery
-> Catto: Thanks Assistant Cat
-> Catto: I'm ready to go!
<<set $packageAssigned = true>>
<<setQuest "Pick up the package">>
===


title: PostOfficeStillProcessing
---
Assistant Cat: Hi Catto, I’m still processing these packages
Assistant Cat: I should have these other packages ready for you soon
<<if $holding == "BakeryPackage">>
Assistant Cat: Could you please deliver your current package to the bakery?
<<else>>
Assistant Cat: In the meantime...
Assistant Cat: Could you please deliver the package that is on my desk to the bakery?
<<endif>>
===

title: PostOfficeReturnedFromBakery
---
Assistant Cat: Oh Catto you’re back!
Assistant Cat: I just finished processing 2 more packages
Assistant Cat: You can deliver them in any order you like
Assistant Cat: None are express so there’s no rush
Assistant Cat: The packages are for the Florist and the Cafe
Assistant Cat: Which package would you like to deliver next?
<<Trigger SceneDoorSwitcher>>
<<jump PostOfficeChooseAnotherPackage>>
===




title: PostOfficeReturnedFromBakeryDemo
---
Assistant Cat: Oh Catto you’re back!
Assistant Cat: I just finished processing a package to the Florist
Assistant Cat: I hear the flowers are really nice this time of year
Assistant Cat: Give them a sniff for me!
<<Trigger PackageFlorist>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
<<Trigger SceneDoorSwitcher>>
===






title: PostOfficeChooseAnotherPackage
---
-> Catto: Cafe <<if $cafePackageDelivered == false>>
	<<jump PostOfficeCafeChosen>>
-> Catto: Florist <<if $floristPackageDelivered == false>>
	<<jump PostOfficeFloristChosen>>
===

title: PostOfficeCafeChosen
---
<<if $cafePackageDelivered == false>>
Assistant Cat: If it’s not too much trouble, I would love a nice drink from the Cafe
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot
Assistant Cat: Anyway, here’s the package for the Cafe!
<<Trigger PackageCafe>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
<<else>>
Assistant Cat: You've already posted that one, choose another
<<jump PostOfficeChooseAnotherPackage>>
<<endif>>
===


title: PostOfficeFloristChosen
---
<<if $floristPackageDelivered == false>>
Assistant Cat: Okay, here’s the package for the Florist!
Assistant Cat: I hear the flowers are really nice this time of year
Assistant Cat: Give them a sniff for me!
<<Trigger PackageFlorist>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
<<else>>
Assistant Cat: You've already posted that one, choose another
<<jump PostOfficeChooseAnotherPackage>>
<<endif>>
===


title: PostOffice3Packages
---
///Needs to deliver package to florist
<<if $holding == "FloristPackage" && $floristPackageDelivered == false>>
Assistant Cat: Your current package needs to go to the florist

///Needs to deliver package to cafe
<<elseif $holding == "CafePackage" && $cafePackageDelivered == false>>
Assistant Cat: Your current package needs to go to the Cafe
Assistant Cat: Don't forget my drink!
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot


///Hasn't picked up the package
<<elseif $holding == "nothing" && $packageAssigned == true>>
Assistant Cat: Don't forget the package on my desk
<<endif>>
===