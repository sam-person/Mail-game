title: MiddayPackages
---
///Needs to deliver package to florist
<<if $holding == "FloristPackage" && $floristPackageDelivered == false>>
Assistant Cat: Your current package needs to go to the florist

///Needs to deliver package to cafe
<<elseif $holding == "CafePackage" && $cafePackageDelivered == false>>
Assistant Cat: Your current package needs to go to the Cafe
Assistant Cat: Don't forget my drink!
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot

///Hasn't picked up the package
<<elseif $holding == "nothing" && $packageAssigned == true>>
Assistant Cat: Don't forget the package on my desk
<<endif>>

//Has delivered package to cafe and has drink
<<if $cafePackageDelivered == true && $cafeDrinkDelivered == false>>
<<jump DeliverCafeDrink>>

<<elseif $cafePackageDelivered == true && $cafeDrinkDelivered == true && $holding == "nothing" && $floristPackageDelivered == false>>
<<jump onlyFloristDeliveryLeft>>

<<elseif $floristPackageDelivered == true && $holding == "nothing" && $cafePackageDelivered == false>>
<<jump onlyCafeDeliveryLeft>>

///Has delivered all packages
<<elseif $floristPackageDelivered == true && $cafePackageDelivered == true>>
<<Trigger SceneDoorSwitcher>>
<<jump ThreePackagesDelivered>>
<<endif>>
===

title: onlyFloristDeliveryLeft
---
Assistant Cat: Well, the only delivery I have left right now is going to the Florist
Assistant Cat: I hear the flowers are really nice this time of year
Assistant Cat: Give them a smell for me!
<<Trigger PackageFlorist>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
===

title: onlyCafeDeliveryLeft
---
Assistant Cat: The only package I have left to deliver right now is going to the cafe
Assistant Cat: If it’s not too much trouble, I would love a nice drink from the Cafe
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot
Assistant Cat: Anyway, here’s the package for the Cafe!
<<Trigger PackageCafe>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
===

title: DeliverCafeDrink
---
<<if $hasCorrectDrink>>
Assistant Cat: You got the drink I wanted!
Assistant Cat: You’re the best, thanks Catto
<<else>>
Assistant Cat: Oh you got me a drink from the Cafe!
Assistant Cat: I see you decided to surprise me
Assistant Cat: This isn’t exactly what I was after, but thank you!
<<endif>>
<<set $cafeDrinkDelivered = true>>
<<jump MiddayPackages>>
===


title: PostOfficeFinishedMidday
---
Assistant Cat: Great work on delivering those packages!
Assistant Cat: I've got another 2 packages for you to deliver now
Assistant Cat: These packages are for the grocery store and the nursing home
Assistant Cat: Which package would you like to deliver next?
<<set $ThreePackagesDelivered = true>>
<<jump PostOfficeChooseAnotherPackageMidday>>
===

title: PostOfficeChooseAnotherPackageMidday
---
-> Catto: Grocery Store
	<<jump PostOfficeGroceryChosen>>
-> Catto: Nursing Home
	<<jump PostOfficeNursingHomeChosen>>
===

title: PostOfficeGroceryChosen
---
Assistant Cat: Okay, here’s the package for the Grocery Store!
Assistant Cat: Make sure you take in that sea breeze while you’re down there
<<Trigger PackageGrocery>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
===

title: PostOfficeNursingHomeChosen
---
Assistant Cat: Okay, here’s the package for the Nursing Home!
Assistant Cat: Grandmeow Cat sometimes doesn’t like to leave her room
Assistant Cat: Good luck with that
<<Trigger PackageNursing>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
===

title: ThreePackagesDelivered
---
<<declare $AfternoonPackagesFinished = false>>
Assistant Cat: Great work on delivering those packages Catto!
Assistant Cat: I've got another 2 packages for you to deliver now
Assistant Cat: These packages are for the grocery store and the nursing home
Assistant Cat: Which package would you like to deliver next?
<<set $ThreePackagesDelivered = true>>
<<jump PostOfficeChooseAnotherPackageMidday>>
===

title: MiddayPackagesDemo
---
<<declare $DemoFinished = false>>
///Needs to deliver package to florist
<<if $holding == "FloristPackage" && $floristPackageDelivered == false>>
Assistant Cat: Your current package needs to go to the florist

///Needs to deliver package to cafe
<<elseif $holding == "CafePackage" && $cafePackageDelivered == false>>
Assistant Cat: Your current package needs to go to the Cafe
Assistant Cat: Don't forget my drink!
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot

///Hasn't picked up the package
<<elseif $holding == "nothing" && $packageAssigned == true>>
Assistant Cat: Don't forget the package on my desk
<<endif>>

//Has delivered package to cafe and has drink
<<if $cafePackageDelivered == true && $cafeDrinkDelivered == false>>
<<jump DeliverCafeDrink>>

<<elseif $cafePackageDelivered == true && $cafeDrinkDelivered == true && $holding == "nothing" && $floristPackageDelivered == false>>
<<jump onlyFloristDeliveryLeft>>

<<elseif $floristPackageDelivered == true && $holding == "nothing">>
<<jump DemoFinished>>

///Has delivered all packages
<<elseif $floristPackageDelivered == true && $cafePackageDelivered == true>>
<<Trigger SceneDoorSwitcher>>
<<jump ThreePackagesDelivered>>
<<endif>>
===

title: DemoFinished
---
<<if $DemoFinished == true>>
<<jump DemoFinished2>>
<<else>>
Assistant Cat: Thanks for delivering that package to the Florist Catto!
Assistant Cat: That's the end of the demo for now
Assistant Cat: Don't forget to wishlist the game if you liked it!!!
<<set $DemoFinished = true>>
<<jump DemoFinished2>>
<<endif>>
===

title: DemoFinished2
---
Assistant Cat: Would you like to finish playing or look around some more?
-> Catto: Finish playing
	<<Trigger DemoFinish>>
-> Catto: Look around
	Assistant Cat: Okay, have fun!
===