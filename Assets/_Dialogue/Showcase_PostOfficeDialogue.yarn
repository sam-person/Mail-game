title: PostOfficeIntro
---
///what package Catto is holding
<<declare $holding = "nothing">>
<<set $holding = "nothing">>
Assistant Cat: Good morning Catto! I hope you slept well
Assistant Cat: Here’s some news for today
Assistant Cat: It’s a beautiful sunny day out there, perfect for delivering some mail! 
Assistant Cat: We’re in peak sunflower season
Assistant Cat: Overnight I got a new leaf on my snapdragon plant!
Assistant Cat: I think that’s all for news
-> Catto: Anything else happening today?
	Assistant Cat: No, I think that’s all. Are you ready to start your day?
	-> Catto: I’m ready to start the day!
	-> Catto: Nothing else, not even any birthdays?
		Assistant Cat: I never forget a birthday. Are you ready to start your day?
		-> Catto: I’m ready to start the day!
-> Catto: Thanks, I’m ready to start the day!
Assistant Cat: I have a few packages I still need to process, I have this one ready to be delivered
<<Trigger PackageBakery>>
<<declare $bakeryPackageDelivered = false>>
<<set $bakeryPackageDelivered = false>>
Assistant Cat: I hope you’ve stretched because you’re going to need to take this one up the hill to the bakery
-> Catto: Thanks <insert name>, I’ll pick up that package and take it to the bakery now
<<setQuest "Pick up the package">>
===


title: PostOfficeStillProcessing
---
Assistant Cat: Hi Catto, I’m still processing these packages. It should be ready after you’ve delivered your current package to the bakery
Assistant Cat: I should have these other packages ready for you soon
<<if $holding == "BakeryPackage">>
Assistant Cat: Could you deliver that package to the bakery?
<<else>>
Assistant Cat: In the meantime, could you please deliver the package that is on my desk to the bakery?
<<endif>>
===


title: PostOfficeReturnedFromBakery
---
Assistant Cat: You’re back! I just finished processing 3 more packages. 
Assistant Cat: You can deliver them in any order you like, none are express so there’s no rush.
Assistant Cat: The packages are for the Florist, the Grocery Store, and the Cafe
Assistant Cat: Which package would you like to deliver next?
<<declare $groceryPackageDelivered = false>>
<<set $groceryPackageDelivered = false>>

<<declare $floristPackageDelivered = false>>
<<set $floristPackageDelivered = false>>

<<declare $cafePackageDelivered = false>>
<<set $cafePackageDelivered = false>>

<<jump PostOfficeChooseAnotherPackageV2>>
===


title: PostOfficeChooseAnotherPackageV2
---
<<declare $packageAssigned = false>>
-> Catto: Cafe
	<<jump PostOfficeCafeChosen>>
-> Catto: Florist
	<<jump PostOfficeFloristChosen>>
-> Catto: Grocery Store
	<<jump PostOfficeGroceryChosen>>
===


title: PostOfficeCafeChosen
---
<<if $cafePackageDelivered == false>>
Assistant Cat: If it’s not too much trouble, I would love a nice drink from the Cafe
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot
Assistant Cat: Anyway, here’s the package for the Cafe!
<<Trigger PackageCafe>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
<<else>>
Assistant Cat: You've already posted that one, choose another
<<jump PostOfficeChooseAnotherPackageV2>>
<<endif>>
===


title: PostOfficeFloristChosen
---
<<if $floristPackageDelivered == false>>
Assistant Cat: Okay, here’s the package for the Florist!
<<Trigger PackageFlorist>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
<<else>>
Assistant Cat: You've already posted that one, choose another
<<jump PostOfficeChooseAnotherPackageV2>>
<<endif>>
===


title: PostOfficeGroceryChosen
---
<<if $groceryPackageDelivered == false>>
Assistant Cat: Okay, here’s the package for the Grocery Store!
<<Trigger PackageGrocery>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
<<else>>
Assistant Cat: You've already posted that one, choose another
<<jump PostOfficeChooseAnotherPackageV2>>
<<endif>>
===


title: PostOfficeChooseAnotherPackage
---
<<if $floristPackageDelivered == false && $cafePackageDelivered == true && $groceryPackageDelivered == false>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: Which package would you like to deliver next?
-> Catto: Florist
	Assistant Cat: Okay, here’s the package for the Florist!
	<<Trigger PackageFlorist>>
	<<setQuest "Pick up the package">>
	<<set $packageAssigned = true>>
-> Catto: Grocery Store
	Assistant Cat: Okay, here’s the package for the Grocery Store!
	<<Trigger PackageGrocery>>
	<<setQuest "Pick up the package">>
	<<set $packageAssigned = true>>
<<elseif $floristPackageDelivered == true && $cafePackageDelivered == false && $groceryPackageDelivered == false>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: Which package would you like to deliver next?
-> Catto: Cafe
	Assistant Cat: Okay, here’s the package for the cafe!
	<<Trigger PackageCafe>>
	<<setQuest "Pick up the package">>
	<<set $packageAssigned = true>>
-> Catto: Grocery Store
	Assistant Cat: Okay, here’s the package for the Grocery Store!
	<<Trigger PackageGrocery>>
	<<setQuest "Pick up the package">>
	<<set $packageAssigned = true>>
<<elseif $floristPackageDelivered == false && $cafePackageDelivered == false && $groceryPackageDelivered == true>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: Which package would you like to deliver next?
-> Catto: Cafe
	Assistant Cat: Okay, here’s the package for the cafe!
	<<Trigger PackageCafe>>
	<<setQuest "Pick up the package">>
	<<set $packageAssigned = true>>
-> Catto: Florist
	Assistant Cat: Okay, here’s the package for the florist!
	<<Trigger PackageFlorist>>
	<<setQuest "Pick up the package">>
	<<set $packageAssigned = true>>
<<elseif $floristPackageDelivered == false && $cafePackageDelivered == true && $groceryPackageDelivered == true>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: That means this one is for the florist
<<Trigger PackageFlorist>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
Assistant Cat: Here you go!
<<elseif $floristPackageDelivered == true && $cafePackageDelivered == false && $groceryPackageDelivered == true>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: That means this one is for the cafe
<<Trigger PackageCafe>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
Assistant Cat: Here you go!
<<elseif $floristPackageDelivered == true && $cafePackageDelivered == true && $groceryPackageDelivered == false>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: That means this one is for the grocery store
<<Trigger PackageGrocery>>
<<setQuest "Pick up the package">>
<<set $packageAssigned = true>>
Assistant Cat: Here you go!
<<endif>>
===


title: PostOffice3Packages
---
<<if $holding == "FloristPackage" && $floristPackageDelivered == false>>
Assistant Cat: Your current package needs to go to the florist

<<elseif $holding == "CafePackage" && $cafePackageDelivered == false>>
Assistant Cat: Your current package needs to go to the Cafe
Assistant Cat: Don't forget my drink!
Assistant Cat: A nice room-temperature lactose-free milk would really hit the spot

<<elseif $holding == "GroceryPackage" && $groceryPackageDelivered == false>>
Assistant Cat: Your current package needs to go to the grocery store

<<elseif $holding == "nothing" && $packageAssigned == true>>
Assistant Cat: Don't forget the package on my desk

<<elseif $holding == "nothing" && $packageAssigned == false>>
Assistant Cat: Nice work getting that package delivered
Assistant Cat: Which package would you like to deliver next?
<<jump PostOfficeChooseAnotherPackageV2>>

<<endif>>
===

title: PostOfficePackageForValerie
---
Assistant Cat: That's all the content for the prototype so far, thanks for playing!
///Assistant Cat: Back already? I’ve just finished processing this package
///Assistant Cat: This package is for Valerie at the Nursing home
===

title: PostOfficeRandom1
---
Assistant Cat: Meow
===